//Jacob Burke

//Moves the player
class PlayerMovement : ZilchComponent
{
    [Property]
    var Speed:Integer = 30;
    
    [Property]
    var JumpSpeed:Integer = 20;
    
    [Property]
    var KeyUp:Keys;
    
    [Property]
    var KeyLeft:Keys;
    
    [Property]
    var KeyRight:Keys;
    
    [Property]
    var KeyDown:Keys;
    
    var Swimming:Boolean = false;
    
    var DoubleJump:Boolean = true;
    
   
    
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        Zero.Connect(this.Owner, Events.CollisionPersisted, this.OnCollisionPersisted);
        Zero.Connect(this.Owner, Events.CollisionEnded, this.OnCollisionEnded);
        Zero.Connect(this.Owner, Events.CollisionStarted, this.OnCollisionStarted);
    }

    function OnCollisionStarted(event : CollisionEvent)
    {
        
        if(event.OtherObject.Name == ("DefaultTile") && this.Owner.GroundChecker.Grounded == true)
        {
        this.DoubleJump = true;
        }
    }

    function OnCollisionEnded(event : CollisionEvent)
    {
        if(event.OtherObject.Region != null)
        this.Swimming = false;
        
    }

    function OnCollisionPersisted(event : CollisionEvent)
    {
        if(event.OtherObject.Region != null)
        this.Swimming = true;
        
        
    }

    function OnLogicUpdate(event : UpdateEvent)
    {
        var direction = Real3.Zero;
        
        var velocity = direction * this.Speed;
        
        if(Zero.Keyboard.KeyIsPressed(this.KeyUp) && this.Owner.GroundChecker.Grounded == true
        && this.Swimming == false)
        {
            this.Owner.RigidBody.Velocity =
            Real3(this.Owner.RigidBody.Velocity.X, Math.Max(this.JumpSpeed, velocity.Y + this.JumpSpeed), 0);
        }
        else if(Zero.Keyboard.KeyIsPressed(this.KeyUp) && this.DoubleJump == true
         && this.Swimming == false)
        {
            
            this.Owner.RigidBody.Velocity =
            Real3(this.Owner.RigidBody.Velocity.X, 
            Math.Max(this.JumpSpeed, velocity.Y + this.JumpSpeed/2), 0);
            this.DoubleJump = false;
            
        }
                
        if(Zero.Keyboard.KeyIsDown(this.KeyLeft))
        {
            direction -= Real3.XAxis;
        }
        
        if(Zero.Keyboard.KeyIsDown(this.KeyRight))
        {
            direction += Real3.XAxis;
        }
        
        if(Zero.Keyboard.KeyIsDown(this.KeyUp) && this.Swimming == true)
        {
            direction += Real3.YAxis;
        }
        
        if(Zero.Keyboard.KeyIsDown(this.KeyDown) && this.Swimming == true)
        {
            direction -= Real3.YAxis;
        }
        
        this.Owner.RigidBody.Velocity = 
        Math.Lerp(this.Owner.RigidBody.Velocity, direction * this.Speed , event.Dt); 
    
    }
}
//THIS IS JEFFERY'S SCRIPT////////////////////////////////////////////////////////////
class GroundChecker : ZilchComponent
{
  [Dependency] var Contact : Contact;
  
  [Property] var GroundAngle : Real = 61;
  
  var AllGrounds : Array[Cog] = new Array[Cog]();
  var Grounded : Boolean { get { return this.AllGrounds.Count > 0; } }
  
  function Initialize(init : CogInitializer)
  {
    Zero.Connect(this.Owner, Events.AnyCollision, this.OnAnyCollision);
  }
  function OnAnyCollision(event : AnyCollisionEvent)
  {
      
    var localUp = Math.Transform(Real3.YAxis, this.Owner.Transform.WorldRotation);
    
    if (!event.Ended && Math.AngleBetween(-event.ContactNormal, localUp) < Math.ToRadians(this.GroundAngle))
    {
      if (this.AllGrounds.FindFirstIndex(event.OtherObject) < 0)
      {
        this.AllGrounds.Add(event.OtherObject);
      }
    }
    else
    {
      this.AllGrounds.RemoveAll(event.OtherObject);
    }
  }
}
//THIS IS JEFFERY'S SCRIPT////////////////////////////////////////////////////////////